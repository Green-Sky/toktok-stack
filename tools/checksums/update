#!/usr/bin/env perl

use strict;
use warnings FATAL => 'all';

sub bazel_path {
   my ($checksum, $path) = @_;
   my $target = $path;
   $target =~ s|^bazel-bin|/|;
   $target =~ s|/([^/]+)$|:$1|;
   ($target, { path => $path, checksum => $checksum })
}

my $CHECKSUMS = 'tools/checksums/linux.sha256';

my %lines =
   map { bazel_path /^(.{64})  (.*)/ }
   do { open my $fh, '<', $CHECKSUMS or die $!; <$fh> };

system ('bazel', 'build', '--config=docker-sandbox', keys %lines) == 0
   or die "bazel failed: $?";

open my $proc, '-|', 'sha256sum', map { $_->{path} } values %lines
   or die "sha256sum failed: $!";
open my $sums, '>', $CHECKSUMS
   or die "could not open $CHECKSUMS for writing: $!";
while (my $line = <$proc>) {
   print $sums $line;
}
